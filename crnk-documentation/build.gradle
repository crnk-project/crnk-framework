apply plugin: 'org.asciidoctor.convert'



asciidoctor {
	backends = ['html5', 'pdf']
	attributes 'source-highlighter': 'coderay'
	sources {
		include 'index.adoc'
	}
}

tasks.assemble.dependsOn 'asciidoctor'

apply plugin: 'org.ajoberstar.git-publish'
// see https://github.com/ajoberstar/gradle-git-publish
gitPublish {

	repoUri = 'https://github.com/crnk-project/crnk-web.git'
	branch = 'master'
	repoDir = file("$buildDir/website/")

	contents {
		from("${project.buildDir}/asciidoc/html5") {
			into('releases/latest/documentation')
		}
		from("${project.buildDir}/asciidoc/pdf") {
			into('releases/latest')
			include "**.pdf"

		}
		rename { String fileName ->
			fileName.replace("index.pdf", "crnk.pdf")
		}
	}

	preserve {
		include '**'
		exclude 'releases/**'
	}

	// message used when committing changes
	commitMessage = 'Publishing a new documentation'
}

gitPublishCopy.doLast {
	def latestSrc = file("${project.projectDir}/src/docs/releases/latest/info.html")
	if (!latestSrc.exists()) {
		latestSrc.parentFile.mkdirs()
		latestSrc.text = ""
	}
}

gitPublishCommit.doFirst {
	def templateFile = file('src/docs/template/release.html')
	def versions = new ArrayList()

	def releasesSrcDir = file("${project.projectDir}/src/docs/releases")
	def releasesWebDir = file("$project.buildDir/website/releases")
	for (def releaseDir : releasesSrcDir.listFiles()) {
		if (releaseDir.isDirectory()) {
			def releaseIndexFile = file("$releasesWebDir/$releaseDir.name/index.html")
			def releaseInfoFile = file("src/docs/releases/${releaseDir.name}/info.html")

			def releaseVersion = releaseDir.name == 'latest' ? project.version : file("src/docs/releases/${releaseDir.name}/version.txt").text

			def info =
					"<div class=\"section\">\n" +
					"  <div class=\"section-content\">\n" +
					"      <h3>$releaseDir.name</h3>" +
					"      <p>Version: ${releaseVersion}</p>" +
					"      <p><a href='documentation/'>Documentation</a> (<a href='crnk.pdf'>pdf</a>)</p>" +
					"      $releaseInfoFile.text\n" +
					"  </div>\n" +
					"</div>"




			def content = templateFile.text.replace('${content}', info)
			content = content.replace('"../crnk', '"../../crnk')

			releaseIndexFile.parentFile.mkdirs()
			releaseIndexFile.text = content
			versions.add(releaseDir.name)
		}
	}

	Collections.sort(versions)
	versions = versions.reverse()
	versions.remove("latest")
	versions.remove("stable")
	versions.add(0, "stable")
	versions.add(1, "latest")

	def releaseSet = new StringBuilder()

	releaseSet.append("<div class=\"section\">")
	releaseSet.append("  <div class=\"section-content\">\n")
	releaseSet.append("    <h3>Releases</h3>\n")
	releaseSet.append("    <ul>\n")
	for (def version : versions) {
		releaseSet.append("   <li><a href=\"$version/\">$version</a></li>\n")
	}
	releaseSet.append("    </ul>\n")
	releaseSet.append("  </div>")
	releaseSet.append("</div>")

	def releasesFile = file("$releasesWebDir/index.html")
	releasesFile.text = templateFile.text.replace('${content}', releaseSet)
}

task promote(type: Copy) {
	into("${project.buildDir}/website/releases/v$BUILD_VERSION_PREFIX")
	from("${project.buildDir}/website/releases/latest")

	doLast {
		def latestSrc = file("${project.projectDir}/src/docs/releases/latest/info.html")

		def versionSrc = file("${project.projectDir}/src/docs/releases/v$BUILD_VERSION_PREFIX/info.html")
		versionSrc.parentFile.mkdirs()
		versionSrc.text = latestSrc.text.replace("<h3>Latest</h3>", "<h3>${project.version}</h3>")
		file("${project.projectDir}/src/docs/releases/v$BUILD_VERSION_PREFIX/version.txt").text = project.version

		def stableSrc = file("${project.projectDir}/src/docs/releases/stable/info.html")
		stableSrc.parentFile.mkdirs()
		stableSrc.text = latestSrc.text.replace("<h3>Latest</h3>", "<h3>${project.version}</h3>")
		file("${project.projectDir}/src/docs/releases/stable/version.txt").text = project.version
	}
}

promote.dependsOn gitPublishCopy
gitPublishCommit.mustRunAfter promote

gitPublishCopy.dependsOn assemble
publish.dependsOn gitPublishPush